class TicTacToe {
  static String playAgain;
  static String bye;
  static String signO;
  static String signX;

  field Board board;
  field String currentSign;

  function void init() {
    let playAgain = "Play again? y/n";
    let signX = "X";
    let signO = "O";
    let bye = "Bye!";

    return;
  }

  function String getSignX() {
    return signX;
  }

  function String getSignO() {
    return signO;
  }

  constructor TicTacToe new() {
    let board = Board.new();
    let currentSign = signX;

    do board.reset();
    do board.draw();

    return this;
  }

  method void run() {
    var boolean exit;
    var String result;
    var char key;

    let exit = false;

    while (~exit) {
      do move();
      do board.draw();

      let result = board.evaluate();

      if ((result = signX) | (result = signO)) {
        do Output.println();
        do Output.printString(result);
        do Output.printString(" Won!");
        let exit = true;
      }
      if (board.getFieldsLeft() = 0) {
        do Output.println();
        do Output.printString("DRAW");
        let exit = true;
      }
    }

    do Output.println();
    do Output.printString(playAgain);

    let key = Keyboard.readChar();

    if ((key = 121) | (key = 89)) {
      do Screen.clearScreen();
      do Output.moveCursor(0, 0);
      do board.reset();
      do board.draw();
      do run();
    } else {
      do Output.println();
      do Output.printString(bye);
      do dispose();
    }

    return;
  }

  method void move() {
    var char key;
    var int position;

    let key = Keyboard.readChar();

    if (key = 49) { let position = 1; }
    if (key = 50) { let position = 2; }
    if (key = 51) { let position = 3; }
    if (key = 52) { let position = 4; }
    if (key = 53) { let position = 5; }
    if (key = 54) { let position = 6; }
    if (key = 55) { let position = 7; }
    if (key = 56) { let position = 8; }
    if (key = 57) { let position = 9; }

    if (position = 0) {
      do move();
    }

    if (board.fieldAvailable(position)) {
      do board.putSign(position, currentSign);
      do switchSign();
    } else {
      do move();
    }

    return;
  }

  method void switchSign() {
    if (currentSign = signX) {
      let currentSign = signO;
    } else {
      let currentSign = signX;
    }
    return;
  }

  method void dispose() {
    do board.dispose();
    do Memory.deAlloc(this);
    return;
  }
}